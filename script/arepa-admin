#!/usr/bin/perl

use strict;
use warnings;
use Carp;
use Getopt::Long;

use Arepa::Config;
use Arepa::BuilderFarm;
use Arepa::Builder::Sbuild;

my $config_file = '/etc/arepa/config.yml';

sub show_help {
    print STDERR <<EOD;
SYNTAX: arepa-admin <command> [options]
where <command> is one of:
  createbuilder
  init
  uninit

arepa-admin createbuilder /var/mybuilder ftp://... squeeze
arepa-admin createbuilder --arch i386 /var/mybuilder ftp://... squeeze

arepa-admin init
arepa-admin init just_this_builder

arepa-admin uninit
arepa-admin uninit just_this_builder
EOD
    exit 1;
}

if (scalar @ARGV == 0) {
    print STDERR "arepa-admin needs a command\n";
    show_help;
}

my $command = shift;
if ($command eq 'createbuilder') {
    my $arch = undef;
    my $r = GetOptions('arch=s' => \$arch);
    if (!$r) {
        print STDERR "Invalid options for build command\n";
        exit 1;
    }

    my ($builder_dir, $mirror, $distribution) = @ARGV;
    if (scalar @ARGV != 3) {
        print STDERR "createbuilder needs exactly three arguments\n";
        show_help;
    }
    Arepa::Builder::Sbuild->create($builder_dir, $mirror, $distribution,
                                   (arch => $arch));
}
elsif ($command eq 'init') {
    Arepa::Builder->ui_module("Arepa::UI::Null");
    my $farm = Arepa::BuilderFarm->new($config_file);
    if (scalar @ARGV) {
        foreach my $builder (@ARGV) {
            $farm->init_builder($builder);
        }
    }
    else {
        $farm->init_builders;
    }
}
elsif ($command eq 'uninit') {
    Arepa::Builder->ui_module("Arepa::UI::Null");
    my $farm = Arepa::BuilderFarm->new($config_file);
    if (scalar @ARGV) {
        foreach my $builder (@ARGV) {
            $farm->uninit_builder($builder);
        }
    }
    else {
        $farm->uninit_builders;
    }
}
else {
    print STDERR "Unknown command '$command'\n";
    exit 1;
}

__END__

=head1 AUTHOR

Esteban Manchado Vel√°zquez <estebanm@opera.com>.

=head1 LICENSE AND COPYRIGHT

This code is offered under the Open Source BSD license.

Copyright (c) 2010, Opera Software. All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

=over 4

=item

Redistributions of source code must retain the above copyright notice,
this list of conditions and the following disclaimer.

=item

Redistributions in binary form must reproduce the above copyright notice,
this list of conditions and the following disclaimer in the documentation
and/or other materials provided with the distribution.

=item

Neither the name of Opera Software nor the names of its contributors may
be used to endorse or promote products derived from this software without
specific prior written permission.

=back

=head1 DISCLAIMER OF WARRANTY

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR
ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON
ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
